#!/usr/bin/env nbb
(ns svgraph-cli
  (:require [clojure.string :as str]
            ["node:fs/promises" :refer [readFile writeFile]]
            ["node:path" :as path]
            ["node:process" :refer [argv stdin stdout stderr exit]]
            ["@xmldom/xmldom" :refer [DOMParser XMLSerializer]]))

(def usage
  "Usage:\n  svgraph svg2dml <input.svg|-> [-o output.xml]\n  svgraph dml2svg <input.xml|-> [-o output.svg]\n  svgraph svg2pptx <input.svg|-> [-o output.pptx]\n  svgraph svgraph <input.svg|-> [-o output.json]\n  svgraph svgraph-presentation <input.svg|-> [-o output.json]\n  svgraph office-causal <input.svg|-> [-o output.json]\n  svgraph office-causal-jsonl <input.svg|-> [-o output.jsonl]\n  svgraph analyze <input.svg|-> [-o output.json]\n  svgraph --version")

(defn fail! [message]
  (throw (js/Error. message)))

(defn arr [value]
  (array-seq (or value #js [])))

(defn patch-selector-api [document]
  (letfn [(find-by-simple-selector [root selector]
            (let [normalized (str/trim selector)
                  name (last (str/split normalized #":"))
                  matches (array)]
              (when-not (re-matches #"^[A-Za-z_][A-Za-z0-9_.:-]*$" normalized)
                (fail! (str "unsupported CLI selector: " selector)))
              (letfn [(visit [node]
                        (when node
                          (when (and (= 1 (.-nodeType node))
                                     (= name (last (str/split (or (.-localName node) (.-nodeName node)) #":"))))
                            (.push matches node))
                          (doseq [child (arr (.-childNodes node))]
                            (visit child))))]
                (visit (or (.-documentElement root) root))
                matches)))
          (install [node]
            (when node
              (when-not (.-querySelectorAll node)
                (set! (.-querySelectorAll node) (fn [selector] (this-as this (find-by-simple-selector this selector)))))
              (when-not (.-querySelector node)
                (set! (.-querySelector node) (fn [selector] (this-as this (aget (find-by-simple-selector this selector) 0)))))
              (doseq [child (arr (.-childNodes node))]
                (install child))))]
    (install document)
    document))

(defn install-dom-shim! []
  (set! (.-__SVGraphBaseDOMParser js/globalThis) DOMParser)
  (set! (.-__svgraphPatchSelectorApi js/globalThis) patch-selector-api)
  (set! (.-DOMParser js/globalThis)
        (js/eval "class QueryDomParser extends globalThis.__SVGraphBaseDOMParser { parseFromString(text, mimeType) { return globalThis.__svgraphPatchSelectorApi(super.parseFromString(text, mimeType)); } }; QueryDomParser"))
  (set! (.-XMLSerializer js/globalThis) XMLSerializer)
  (set! (.-Node js/globalThis) #js {:ELEMENT_NODE 1 :TEXT_NODE 3}))

(defn parse-io [args]
  (loop [remaining args
         input "-"
         output "-"]
    (if (empty? remaining)
      (do
        (when (str/blank? input) (fail! "missing input path"))
        (when (str/blank? output) (fail! "missing output path"))
        {:input input :output output})
      (let [arg (first remaining)]
        (cond
          (or (= arg "-o") (= arg "--output"))
          (recur (nnext remaining) input (or (second remaining) ""))

          (or (str/blank? input) (= input "-"))
          (recur (next remaining) arg output)

          :else
          (fail! (str "unexpected argument: " arg)))))))

(defn read-stdin []
  (js/Promise.
   (fn [resolve reject]
     (let [chunks (array)]
       (.on stdin "data" (fn [chunk] (.push chunks chunk)))
       (.on stdin "error" reject)
       (.on stdin "end" (fn [] (resolve (.toString (.concat js/Buffer chunks) "utf8"))))))))

(defn read-input [path]
  (if (= path "-")
    (read-stdin)
    (readFile path "utf8")))

(defn write-output [path value]
  (if (= path "-")
    (do (.write stdout value) (js/Promise.resolve nil))
    (writeFile path value)))

(defn json [value]
  (str (js/JSON.stringify value nil 2) "\n"))

(defn script-path []
  (or (some #(when (str/ends-with? (str %) "svgraph.cljs") %) (arr argv))
      (aget argv 1)
      "bin/svgraph.cljs"))

(defn cli-args []
  (let [items (vec (arr argv))
        script (script-path)
        index (.indexOf (clj->js items) script)]
    (if (neg? index)
      (vec (drop 2 items))
      (subvec items (inc index)))))

(defn run-command [app command text]
  (case command
    "svg2dml" ((aget app "svgToDrawingMl") text)
    "dml2svg" ((aget app "drawingMlToSvg") text)
    "svg2pptx" ((aget app "svgToPptx") text)
    "svgraph" (json ((aget app "buildSVGraph") text))
    "svgraph-presentation" (json (.-presentation ((aget app "buildSVGraph") text)))
    "office-causal" (json ((aget app "buildOfficeCausalPayload") ((aget app "buildSVGraph") text)))
    "office-causal-jsonl" ((aget app "buildOfficeCausalJsonl") ((aget app "buildSVGraph") text))
    "analyze" (json (.-coverage ((aget app "buildSVGraph") text)))
    (fail! (str "unknown command: " command))))

(defn main! []
  (install-dom-shim!)
  (let [args (cli-args)
        command (first args)]
    (cond
      (or (nil? command) (= command "-h") (= command "--help"))
      (do (.write stdout (str usage "\n")) (exit 0))

      (= command "--version")
      (-> (readFile (.resolve path (.dirname path (script-path)) "../package.json") "utf8")
          (.then (fn [text]
                   (.write stdout (str "svgraph " (.-version (js/JSON.parse text)) "\n"))
                   (exit 0))))

      :else
      (let [{:keys [input output]} (parse-io (subvec args 1))]
        (-> (js/import "../docs/app.js")
            (.then (fn [app]
                     (-> (read-input input)
                         (.then (fn [text]
                                  (write-output output (run-command app command text))))))))))))

(defn report-error! [error]
  (.write stderr (str (or (.-message error) error) "\n" usage "\n"))
  (exit 1))

(try
  (-> (main!)
      (.catch report-error!))
  (catch :default error
    (report-error! error)))
